# 汇编语言程序设计上机实验八

## 题目描述

>   编写一个被0除的中断处理程序并测试，当被0除时，显示一个提示信息 “Divided by Zero", 中断类型号为0.

## 分析思路

我们需要做的就是三步。

1.  写一个程序可以输出"Divided by Zero"
2.  将该程序的指令拷贝到安全的空间`0000:0200h~0000:02ffh`中。
3.  将入口地址`0000:0200`存储到中断向量表的第0项中。

### 第一步

在屏幕中显示字符串的方法，我已知的有两种，一种是将数据写到`b800:0000`这段显存空间中，另一种是使用`mov ah 2; int 21h`这种形式的输出。可以两种都尝试一下。

### 第二步

可以使用`movsb`串传送指令，将写好的中断处理程序的指令拷贝到`0000:0200`内存单元处。

### 第三步

这步是最简单的。

```asm
mov ax, 0
mov es, ax
mov byte ptr es:[0], 200h ; 低地址存放IP
mov byte ptr es:[2], 0 ; 高地址存放CS
```

## 编写代码

### 输出字符串的中断程序

#### 将数据传送到显存`b800h:0000`处输出。

```asm
div0:
	jmp short div0_start ; 该指令占用两个字节
	db "Divided by Zero!" ; 存放需要输出的数据
	
div0_start:
	; 由于该中断处理程序后面会直接退出，所以这里不再将寄存器保存在栈中。
	
	mov ax, cs
	mov ds, ax
	mov si, 202h ; 数据存放的位置的偏移地址
	
	mov ax, b800h
	mov es, ax
	
	mov di, 12*160 + 32*2 ; 选择在屏幕的中间处输出
	mov al, 00000100b ; 选择输出的格式为红字
	
	mov cx, 16 ; 总共需要输出16个字符
output:
	mov ah, [si]
	mov es:[di], ah ; 偶数地址设置字符
	mov es:[di].1, al ; 奇数地址设置格式
	
	inc si
	add di, 2
	loop output
	
	mov ax, 4c00h
	int 21h ; 程序结束
	
div0_end: nop ; 为了计算div0中断程序占用了多少字节
; 占用的字节数为 offset div0_end - offset div0


```

#### 使用`mov ah, 2; int 21h`输出

```asm
div0:
	jmp short div0_start ; 该指令占用两个字节
	db "Divided by Zero!" ; 存放需要输出的数据
	
div0_start:
	; 由于该中断处理程序后面会直接退出，所以这里不再将寄存器保存在栈中。
	
	mov ax, cs
	mov ds, ax
	mov si, 202h ; 数据存放的位置的偏移地址

	mov ah, 2	
	mov cx, 16 ; 总共需要输出16个字符
output:
	mov dl, [si] ; 要输出的字符放到dl中
	int 21h ; 输出

	inc si
	loop output
	
	mov ax, 4c00h
	int 21h ; 程序结束
	
div0_end: nop ; 为了计算div0中断程序占用了多少字节
; 占用的字节数为 offset div0_end - offset div0
```

### 复制中断程序到`0000:0200`处

```asm
mov ax, cs
mov ds, ax
mov si, offset div0 ; ds:si 指向div0中断处理程序

mov ax, 0
mov es, ax
mov di, 200h ; es:si 指向0000:0200处

mov cx, offset div0_end - offset div0 ; 需要复制的字节数

cld ; 设置串传送时， si，di递增

rep movsb ; 将 ds:si 处的数据复制到 es:di处
```

### 设置中断表

```asm
mov ax, 0
mov es, 0
mov word ptr es:[0], 200h
mov word ptr es:[2], 0 ; 指向中断处理程序 0:200h处
```

### 完整代码展示

```asm
stack segment stack
    db 128 dup (?)
stack ends

code segment
    assume cs:code, ss:stack
start:
    mov ax, cs
    mov ds, ax
    mov si, offset div0 ; ds:si 指向div0中断处理程序

    mov ax, 0
    mov es, ax
    mov di, 200h ; es:si 指向0000:0200处

    mov cx, offset div0_end - offset div0 ; 需要复制的字节数

    cld ; 设置串传送时， si，di递增

    rep movsb ; 将 ds:si 处的数据复制到 es:di处

    ; 设置中断表
    mov word ptr es:[0], 200h
    mov word ptr es:[2], 0


    mov ax, 1000h
    mov bh, 1
    div bh

    mov ax, 4c00h
    int 21h

div0:
    jmp short div0_start ; 该指令占用两个字节
    db "Divided by Zero!" ; 存放需要输出的数据

div0_start:
    ; 由于该中断处理程序后面会直接退出，所以这里不再将寄存器保存在栈中。

    mov ax, cs
    mov ds, ax
    mov si, 202h ; 数据存放的位置的偏移地址

    mov ah, 2
    mov cx, 16 ; 总共需要输出16个字符
output:
    mov dl, [si] ; 要输出的字符放到dl中
    int 21h ; 输出

    inc si
    loop output

    mov ax, 4c00h
    int 21h ; 程序结束

div0_end: nop ; 为了计算div0中断程序占用了多少字节
    ; 占用的字节数为 offset div0_end - offset div0

code ends
end start
```

## 运行结果

在上述的完整代码中，我们使用的是`mov ah, 2`的方式来输出。

还使用了一段程序

```asm
mov ax, 1000h
mov bh, 1
div bh
```

这个程序必然会导致除法溢出。

编译运行结果如下图所示：

![image-20200606171752161](/home/sher/.config/Typora/typora-user-images/image-20200606171752161.png)

![image-20200606171916218](/home/sher/.config/Typora/typora-user-images/image-20200606171916218.png)

## 调试程序

先看一下拷贝数据之前内容空间的内容。

![image-20200606172416655](/home/sher/.config/Typora/typora-user-images/image-20200606172416655.png)

![image-20200606172437058](/home/sher/.config/Typora/typora-user-images/image-20200606172437058.png)

![image-20200606172831745](/home/sher/.config/Typora/typora-user-images/image-20200606172831745.png)

执行完串传送指令之后

![image-20200606172928407](/home/sher/.config/Typora/typora-user-images/image-20200606172928407.png)

设置完中断表后，中断表的第0项被设置为 `0000:0200`

![image-20200606173036630](/home/sher/.config/Typora/typora-user-images/image-20200606173036630.png)

到了之后产生除法溢出之后，CPU触发0号中断，转移到`0000:0200`处执行代码。

![image-20200606173157816](/home/sher/.config/Typora/typora-user-images/image-20200606173157816.png)

之后就是循环输出字符信息了。

![image-20200606173253241](/home/sher/.config/Typora/typora-user-images/image-20200606173253241.png)