# 第十一章 标志寄存器

标志寄存器（flag）是我们要学习的最后的一个寄存器，flag寄存器是按位起作用的，也就是说，它的每一位都有专门的含义，记录特定的信息。

![image-20200605170621593](/home/sher/.config/Typora/typora-user-images/image-20200605170621593.png)

## ZF标志

flag寄存器的第六位为ZF，零标志位。

**`ZF`用来标志相关指令的计算结果是否为0。**如果为0，zf为1，如果不是0，zf为0.在计算机中1表示逻辑真。

例如：

```asm
mov ax, 1
sub ax, 1
```

执行之后，结果为0，则zf=1.

```asm
mov ax, 1
or ax, 0
```

执行之后结果不为0， 则zf=0.

只有`add, sub, mul, div, inc, or, and`等这些计算的指令会影响zf标志位。

## PF 标志

flag的第二位为PF，**奇偶标志位，记录结果中的1的个数是否为偶数。**

**如果结果的1的个数为偶数，则PF为1，如果为奇数，则PF为0。**

```asm
mov al, 1
add al, 10
```

结果为00001011B，其中有3个1，则pf=0

```asm
mov al, 1
or al, 2
```

结果为 00000011B，其中有2个1，则pf=1

## SF 标志

flag的第七位为PF，符号标志位，**记录结果是否为负数。**

**如果结果为负数，SF为1，如果非负，SF为0**

在计算机中，负数使用补码的方式存储，也就是说有时候一个运算既可以看作是有符号运算，也可以看作是无符号运算。

```asm
mov al, 10000001b
add al, 1
```

执行之后，结果为1000010b, sf = 1，如果指令进行的是有符号运算，结果为负。

```asm
mov al, 10000001b
add al, 01111111b
```

执行之后，结果为0, sf = 0，如果进行的是有符号运算，结果非负。

某些指令执行之后会影响，标志寄存器中的多个标志位。

## CF 标志

flag的第0位是CF，进位标志位。**在进行无符号运算的时候，记录是否像最高有效位的更高位产生进位。**

**如果结果产生了进位，CF为1，没有产生进位，CF为0**。

```asm
mov al, 98h
add al, al
```

执行之后， al为30h， cf=1。

之前我们说的产生的溢出的信息并没有消失，而且放在了CF标志中。

两个数据做减法的时候，有可能像更高位借位。

```asm
mov al, 97h
sub al, 98h
```

执行之后，al为ffh，cf=1。

## OF 标志

flag的第11位是OF，溢出标志位。**OF记录了有符号运算的结果是否产生了溢出，如果发生溢出，OF为1，如果没有发生溢出，OF为0.**

注意：**CF是对无符号运算有效的标志位，OF是对有符号运算有效的标志位。**一个是进位，一个是溢出。完全不一样。

例：

```asm
mov al, 98
add al, 99
```

对于无符号运算来说，结果为，197小于255，并没有产生向最高位的更高位进位，因此CF为0,。对于有符号运算来说，197大于127，此时溢出了，因此OF为1.

```asm
mov al, 0f0h
add al, 88h
```

指令执行之后，cf为1， of为1.

```asm
mov al, 0f0h
add al, 78h
```

指令执行之后，cf为1， of为0.

## adc指令

adb执行是进位加法指令，利用了cf位上记录的进位值。

命令格式：adc 操作对象1 操作对象2

功能：操作对象1 = 操作对象1 + 操作对象2 + cf

```asm
mov ax, 2
mov bx, 1
sub bx, ax
adc ax, 1
```

指令之后，ax = ax + 1 + cf = 2 + 1 + 1 = 4

```asm
mov ax, 1
add ax, ax
add ax, 3
```

执行之后，ax = ax + 3 + cf =  2 + 3 + 0 = 5

```asm
mov al, 98h
add al, al
add al, 3
```

执行之后， ax = ax + 3 + cf = 30h + 3 + 1 = 34h

**所谓的adc就是add cf。**

### adc指令的作用

编程，计算1EF00H+201000H，结果放在ax（高16位）和bx（低16）中。

使用add和adc的组合可以很轻松的完成这个任务。

两个数的位数都大于16，我们可以先将低16位相加，然后讲高16位和进位值相加。

```asm
mov ax, 001eh
mov bx, 0f000h
add bx, 1000h ; 低16位相加，这里可能会产生进位
adc ax, 0020h ; 高16位相加，同时加上上面那条指令可能产生的进位值
```

编程，计算1e f000 1000h + 20 1000 1ef0h，结果放在ax（最高的16位），bx（次高的16位）和cx（低16位）中。

使用和上面同样的思路。

```asm
mov ax, 001eh
mov bx, 0f000h
mov cx, 1000h
add cx, 1ef0h
adc bx, 1000h
adc ax, 0020h
```

编写一个子程序，对两个128位的数据进行相加。

名称：add128

功能：两个128位数据相加

参数：

​	ds:si指向存储第一个数的内容空间（8个字）由低地址到高地址依次存放。

​	ds:di指向存储第一个数的内容空间。

​	结果放在第一个数的存储空间中。

```asm
add128:
	push ax
	push cx
	push si
	push di
	
	sub ax, ax  ; 将cf设置为0，防止之前的cf影响到第一次adc。
	
	mov cx, 8 ; 每次都进行16位的加法，需要进行8次。
s:
	mov ax, [si]
	adc ax, [di]
	mov [si], ax
	
	inc si
	inc si ; add si, 2 一次处理两个字节
	inc di
	inc di ; add di, 2 但是这里不能写成 add 的形式
	
	loop s
	
	pop di
	pop si
	pop cx
	pop ax
	ret
```

注意：**inc和loop指令不会影响cf位。**上面不可以写成`add si, 2`这种形式，因为add指令会影响cf位，会使得第12行的加法产生的进位信息丢失！！

## sbb 指令

带借位的减法指令，利用了cf位上记录的信息。

指令格式：sbb 操作对象1 操作对象2

功能： 操作对象1 = 操作对象1 - 操作对象2 - cf

和adc指令类似，使用sbb指令可以显示大数据的减法。

编程，计算 003e1000h - 00202000h， 结果放在ax和bx中。

```asm
mov ax, 003eh
mov bx, 1000h
sbb bx, 2000h ; 这里产生了借位， cf = 1
sbb ax, 0020h ;  ax = ax - 0020h - 1 减去上面产生的借位
```

## cmp 指令

**cmp是比较指令，功能相当于是减法指令，但是不保存结果，只对标志寄存器产生影响。**

指令格式： cmp 操作对象1， 操作对象2

功能：计算操作对象1 - 操作对象2，但是不保存结果，只改变标志寄存器。

使用`cmp ax, bx`之后，看标志寄存器，就知道ax和bx之间的大小关系。

-   zf = 0  => ax == bx
-   zf != 0 => ax != bx
-   cf = 1  => ax < bx（不够减，产生了借位）
-   cf = 0  => ax >= bx
-   zf !=0, cf = 0 => ax > bx
-   zf !=0, cf = 1 =>  ax < bx

上面都是对于无符号运算来说的，对于有符号运算来说，我们需要看的是sf和of则两个标志位。

对于有符号运算 `cmp ah, bh`来说

-   sf = 1, of = 0 => 没有产生溢出，结果还为负数，ah < bh
-   sf = 1, of = 1 => 因为溢出导致结果为负数，原结果一定为正数， ah > bh
-   sf = 0, of = 1 => 溢出结果为正数，原结果一定为负数，ah < bh
-   sf = 0, of = 0 => 没有产生溢出，结果为正数，ah > bh
-   相等还是使用zf来进行判断。

## 检查比较结果的条件跳转指令

下面是常见的根据无符号数比较结果进行转移的指令。

| 指令 |      含义      |   检测标志位    |
| :--: | :------------: | :-------------: |
|  je  |   jump equal   |      zf=1       |
| jne  | jump not equal |      zf=0       |
|  jb  |   jump below   |      cf=1       |
| jnb  | jump not below |     cf = 0      |
|  ja  |   jump above   | zf != 0且cf = 0 |
| jna  | jump not above | zf = 0或cf = 1  |

这些指令一般都是配合`cmp`指令一起使用。

编程，如果ah == bh， ah = ah + ah，否则ah = ah + bh

放在C语言中也就是

```c
if (ah == bh) {
    ah += ah
} else {
    ah += bh
}
```

其实直接加bx不也是一样的嘛。。。不用关注这些细节。

```asm
	cmp ah, bh
	jne s
	add ah, ah ; 相当于if中的内容
	jmp short ok ; 执行了if之后不要执行else，使用jmp指令跳过else
s:
	add ah, bh ; 相当于else中的内容
ok: 
	...
```

## DF 标志和串传送指令

flag的第十位是DF，方向标志位。**在串处理指令中，控制每次操作的si，di的增减。*

-   df = 0，每次操作之后si，di递增
-   df = 1，每次操作之后si，di递减

下面是几个串传送指令。

### movsb

指令格式： movsb

功能：执行movsb指令之后相当于进行下面的几步操作。

```asm
mov es:[di], byte ptr ds:[si] ; 8086并不支持这样的指令，这里只是描述

如果df == 0， 递增
inc si
inc di

如果df == 1， 递减
dec si
dec di
```

### movsw

和movsb类似，movsb是传送一个byte，而movsw是传送一个word。相当于以下的指令。

```asm
mov es:[di], word ptr ds:[si] ; 该指令8086不支持，只是描述

如果df == 0， 递增
add si, 2
add di, 2

如果df == 1，递减
sub si, 2
sub di, 2
```

movsb和movsw都是串传送的一个步骤，一般来说movsb和movsw都和rep配合使用。

```asm
rep movsb
```

相当于

```asm
s:
	movsb
	loop s
```

由此可见`rep movsb`可以实现`cx`个字符的传送。

### 设置df标志位

8086cpu提供了两条指令来对df位进行设置。

-   cld指令，将df置0
-   std指令，将df置1

编程，使用串传送指令，将data段的第一个字符串复制到它后面的空间中。

```asm
assume cs:code, ds:data

data segment
	db 'Welcome to masm!'
	db 16 dup (0)
data ends

code segment
start:
	mov ax, data
	mov ds, ax
	mov si, 0 ; ds:si 指向第一段数据
	
	mov es, ax
	mov di, 16 ; es:di 指向后面的空间
	
	mov cx, 8 ; 一次复制一个字，总共需要8次
	cld ; 设置si， di递增
	rep movsw
	
	; 因为需要复制的刚好是16字节8个字，所以可以使用movsw，
	; 如果是奇数字节就需要送movsb了
	; mov cx, 16
	; cld
	; rep movsb
	
	mov ax, 4c00h
	int 21h
code ends
end start
```

## pushf 和 popf

pushf的功能是将标志寄存器的值入栈，而popf是从栈中弹出数据，送入标志寄存器中。

```asm
mov ax, 0
push ax
popf ; 将标志寄存器的所有位置0
mov ax, 0fff0h
add ax, 0010h ; 结果是 0010h
pushf
pop ax ; ax变成了标志寄存器的值
and al, 11000101b
and ah, 00001000b
```

上述程序执行之后，ax为？

也就是看`add ax, 0010h`之后，几个标志位的值。

也就是`0fff0h + 0010h` 结果为`0`。

看后面的一个and，只需要看第0， 2， 6， 7， 11位的值

-   第0位CF，无符号运算，产生了进位，CF = 1
-   第2位PF， 有偶数个0，PF = 1
-   第6位ZF，结果为0， ZF = 1
-   第7位SF，结果不为负数，SF = 0
-   第11位OF，有符号运算，没有产生溢出，OF = 0

最后得到的ax为0045h。

## 编写一个子程序

名称：letterc

功能：讲以0结尾的字符串中的小写字母转变成大写字母

参数：ds:si指向字符串的首字母

```asm
assume cs:code, ds:data, ss:stack

data segment
	db "Beginner's All-purpose Symbolic Instruction Code.", 0
data ends

stack segment stack
	db 128 dup (0)
stack ends

code segment
start:
	mov ax, data
	mov ds, ax
	mov si, 0
	
	mov ax, stack
	mov ss, ax
	mov sp, 128
	
	call letterc
	
	mov ax, 4c00h
	int 21h
	
letterc:
	push si
	push cx
	mov ch, 0
change:
	mov cl, [si] ; 到达字符串的末尾
	jcxz ok
	
	cmp byte ptr [si], 'a'
	jnb min ; 如果大于等于 ‘a’跳转到min
	
	inc si
	loop change ; 判断下一个字符

min:
	cmp byte ptr [si], 'z' ; 如果小于等于’z'，那么就是小写字母，跳转到upper使它变成大写字母。
	jna upper
	
	inc si
	loop change ; 判断下一个字符
	
upper: ; 讲ds:si指向变成大写字母
	push ax
	
	mov al, [si]
	and al, 11011111b ; 将第五位变成0
	mov [si], al
	
	pop ax
	
	inc si
	loop change ; 判断下一个字符

ok:
	pop cx
	pop si
	ret
	
code ends
end start
```

上面的写法似乎有点儿太过于复杂了，其实只需要使用几个cpm就可以解决这个问题了。

```asm
assume cs:code, ds:data, ss:stack

data segment
	db "Beginner's All-purpose Symbolic Instruction Code.", 0
data ends

stack segment stack
	db 128 dup (0)
stack ends

code segment
start:
	mov ax, data
	mov ds, ax
	mov si, 0
	
	mov ax, stack
	mov ss, ax
	mov sp, 128
	
	call letterc
	
	mov ax, 4c00h
	int 21h
	
letterc:
	push si
change:
	cmp byte ptr[si], 0
	je ok
	cmp byte ptr[si], 'a'
	jb next
	cmp byte ptr[si], 'z'
	ja next
	sub byte ptr [si], 20h ; 小写变成大写减去20h
next:
	inc si
	jmp short change
	
ok:
	pop si
	ret

code ends
end start
```

这样写更加简单明了了！

