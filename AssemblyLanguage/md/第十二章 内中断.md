# 第十二章 内中断

## 内中断的产生

-   除法错误：0
-   单步执行：1
-   into命令：4
-   int指令：该指令格式为int n， 其中n就是中断类型码

## 中断向量表

8086cpu的中断向量表存放在0000:0000到0000:03ff这段空间中，其中一个表项占用两个字，低地址存放偏移地址，高地址存放段地址。

## 中断过程

-   从中断信息中获取中断码。
-   标志寄存器的值入栈。（中断过程中会改变标志寄存器的值）
-   设置标志寄存器第8为TF和第9位IF的值为0
-   cs的内容入栈
-   ip的内容入栈
-   从内存地址为 `中断类型码*4 和中断类型码*4 + 2`的两个字单元中读取中断处理程序的入口地址设置的ip和cs。

简洁点来说就是

-   获取中断码N
-   pushf
-   tf = 0, if = 0
-   push cs
-   push ip
-   (ip) = (N×4)， (cs) = (N×4 +2) 

然后cs和ip被改变，cpu开始执行由程序员编写的中断处理程序。

## 中断处理程序和iret

中断处理程序实际上和之前学的子程序是一样的，都是返回不是使用ret返回，而是使用iret返回，多了一个标志寄存器出栈的过程。

```asm
iret

相当于
pop is
pop cs
popf ; 当然不是真正的相当于，只是一个描述
```

## 处理0号中断

编程编写中断处理程序，当中断0发生时，显示"overflow!"。

我们将这段程序称之为`do0`。

出于简单的考虑，我们将这段程序放在`0000:200~0000:2ff`这段没有人使用的空间中。

### 编写该中断处理程序的步骤

-   编写可以显示“overflow！”的中断处理程序。
-   将该程序送入到`0000:200~0000:2ff`这段内容空间中。
-   将do0入口地址`0000:200`写入到中断向量表的第0项中。

### 将一段程序复制到0:200处

```asm
assume cs:code
code segment
start:
	mov ax, cs
	mov ds, ax
	mov si, offset do0
	
	mov ax, 0
	mov es, ax
	mov di, 200h
	
	; 使用编译器帮我们计算do0中所有指令的字节数
	mov cx, offset do0end - offset do0
	
	cld 
	rep movsb ; 串传送指令，讲ds段数据传送到es段中。
	
	; 设置中断向量表
	
	mov ax, 4c00h
	int 21h
	
do0: 
	; 显示字符串 “overflow！”
	mov ax, 4c00h
	int 21h
	
do0end: nop

code ends
end start
```



### 编写do0程序

```asm
do0:
	jmp short do0start ; 该指令占两个字节
	db "overflow!" ; 保存数据

do0start:
	mov ax, cs
	mov ds, ax
	mov si, 202h ;指向数据 overflow
	
	mov ax, 0b800h
	mov es, ax
	mov di, 12*160+36*2
	
	mov cx, 9
s:
	mov al, [si]
	mov es:[di], al ; 将字符输出到屏幕中
	inc si
	add di, 2 ; 下一个字符
	loop s
	
	mov ax, 4c00h
	int 21h

do0end: nop

code ends
end start
```

### 设置中断向量

```asm
mov ax, 0
mov es, ax
mov word ptr es:[0], 200h
mov word ptr es:[2], 0
```

### 整个程序

```asm
stack segment stack
    db 128 dup (?)
stack ends

code segment
    assume cs:code, ss:stack
start:
	mov ax, cs
	mov ds, ax
	mov si, offset do0
	
	mov ax, 0
	mov es, ax
	mov di, 200h
	
	; 使用编译器帮我们计算do0中所有指令的字节数
	mov cx, offset do0end - offset do0
	
	cld 
	rep movsb ; 串传送指令，讲ds段数据传送到es段中。
	
	; 设置中断向量表
    mov word ptr es:[0], 200h
    mov word ptr es:[2], 0

    mov ax, 1000h
    mov bh, 1
    div bh
	
	mov ax, 4c00h
	int 21h
	
do0:
	jmp short do0start ; 该指令占两个字节
	db "overflow!" ; 保存数据

do0start:
	mov ax, cs
	mov ds, ax
	mov si, 202h ;指向数据 overflow
	
	mov ax, 0b800h
	mov es, ax
	mov di, 12*160+36*2
	
	mov cx, 9
s:
	mov al, [si]
	mov es:[di], al ; 将字符输出到屏幕中
    mov byte ptr es:[di+1], 00000100b
	inc si
	add di, 2 ; 下一个字符
	loop s
	
	mov ax, 4c00h
	int 21h

do0end: nop

code ends
end start
```

